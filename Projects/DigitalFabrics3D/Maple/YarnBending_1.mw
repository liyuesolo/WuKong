restart: with(linalg):with(codegen):
toC := proc(expr, file)
try fremove(file) catch: end try:
writeto(file):
C(expr,optimized):
writeto(terminal):
end:

#For initially straight rods without twisting

x := matrix(3,3):


x0 := vector(3, [x[1,1], x[1,2], 0]):
x1 := vector(3, [x[2,1], x[2,2], 0]):
x2 := vector(3, [x[3,1], x[3,2], 0]):

u0 := x[1, 3]:
u1 := x[2, 3]:
u2 := x[3, 3]:

d1 := matadd(x1,-x0):
d1N := d1 / sqrt(dotprod(d1, d1, 'orthogonal')):
d2 := matadd(x0,-x2):
d2N := d2 / sqrt(dotprod(d2, d2, 'orthogonal')):

cos_theta := dotprod(d1N, d2N, 'orthogonal'):
sin_theta := crossprod(d1N, d2N)[3]:
theta := 2 * arctan(sin_theta, (1.0 + cos_theta)):

W := kb * theta * theta / (u1 - u2):


vx := vector(9,[x0[1],x0[2], u0, x1[1],x1[2],u1, x2[1],x2[2],u2]):

F := grad(-W,vx):
J := jacobian(F,vx):
V := array(1..1):
V[1] := W:


toC(V,"YarnBendV.mcg"):
toC(F,"YarnBendF.mcg"):
toC(J,"YarnBendJ.mcg"):


NULL;

